name form to email

use {

  ext
  http
  jade
  mustache

  validator form {

    rule {
      path name
      required
    }

    rule {
      path topic
      required
    }

    rule {
      path email
      required
    }

  }

  smtp {
    host mailtrap.io
    port 2525
    username 18935d013e9ac844f
    password ***REMOVED***
  }

}

flow main {

  put form {

    name {
      placeholder write your name
    }

    topic {
      placeholder what is the topic?
    }

    question {
      placeholder put your question here 
      type textarea
    }

    email {
      placeholder email address
      type email
    }

  }

  http/router {

    GET / {
      run render form
    }

    POST / {

      form/validate request.data {

        when valid {

          run/parallel {

            run {

              jade html.main <<- jade
                h2 thanks for the message!
                a(href='/') &larr; back to home page
              ---

              run render layout
            }

            run {

              mustache email.body <<- mustache
                name: {{ request.data.name }}
                topic: {{ request.data.topic }}
                question: {{ request.data.question }}
              ---
              
              smtp/send {
                from ":request.data.name" <form2email@nicksellen.co.uk>
                to nigel@blah.com
                reply-to :request.data.email
                subject new message from [:request.data.name]
                body :email.body
              }

              halt!

            }

          }    
        }

        when invalid {
          
          run render form

          putvars response {
            status 400
          }

        }

      }
      
    }

  }

}

flow render form {

  jade html.main <<- jade

    h1 write me a question!

    form(action='/', method='POST')

      each details, name in form

        has_errors = errors && errors[name] 

        val = request && request.data ? request.data[name] : ''

        .form-group(class=has_errors ? 'has-error' : '')
          if details.type == 'textarea'
            textarea.form-control.input-lg(name=name, placeholder=details.placeholder)= val
          else
            t = details.type != null ? details.type : 'text'
            input.form-control.input-lg(type=t, name=name, value=val, placeholder=details.placeholder)

          if has_errors
            each error in errors[name]
              span.help-block= error

      .form-group
        button.btn.btn-lg.btn-success(type='submit') send!

  ---  

  run render layout 

}

flow render layout {

  jade <<- jade
      !!!
      html
        head
          link(rel='stylesheet', href='//netdna.bootstrapcdn.com/bootstrap/3.1.1/css/bootstrap.min.css')
        body
          .container
            .row
              .col-sm-3
              .col-sm-6

                != html.main

    ---
}

trigger {
  http localhost:6070 {
    run main
  }
}
