name reka/ui

version 10.2

use {
  http
  reka
  mustache
  jade
  filesystem fs {
    dir data-apps
  }
}

run http {

  http/router {

    group static HTML with layout {
      
      GET /who {
        put html.main @include(pages/who.md)
      }
      
      GET /how {
        put html.main @include(pages/how.md)
      }
      
      run render layout
    }  
    
    group running apps {

      group with layout {
        
        GET / {
          reka/list
          put html.sidebar @include(running/list/sidebar.md)
          jade html.main   @include(running/list/main.jade)
        }

        GET /apps/:id* {
        
          reka/get app {
            app :id
          }
          
          then {
            put html.sidebar @include(running/show/sidebar.md)
            jade html.main   @include(running/show/main.jade)
          }
        }

        GET /apps/:id*/flows/:flow* {

          reka/visualize {
            app :id
            flow :flow
            format svg
            out visualization
          }
          
          reka/get app {
            app :id
          }
          
          put extrastyles @include(visualize-extras.html)
          
          put {
            layout wide
          }
        
          jade html.main <<- jade
          
            != extrastyles
            
            ul.inline-list.flow-list
              each fl in app.flows
                li(id="flow-#{fl}", class=flow==fl ? 'active' : '')
                  h3
                    a(href="/apps/#{id}/flows/#{fl}")= fl
            
            .svg-container
              .inner!= visualization
            
          ---  
        
        }
          
        run render layout
      }
      
      GET /apps/:id*/flows/:flow*.:format {
        reka/visualize {
          app :id
          flow :flow
          format :format
        }
      }

      PUT /apps/:id*/redeploy {
        reka/redeploy :id
        http/redirect :request.headers.Referer
      }
    
      DELETE /apps/:id* {
        reka/undeploy :id
        http/redirect /
      }
    }
    
    group static HTTP assets {
      GET /css/app.css @include(app.css)
    }
  
  }

}

run render layout {
  
  put menu {
    running {
      icon fa-play
      title apps
      path /
    }
  }

  put utilmenu {
    how {
      icon fa-question
      title how does it work?
      path /how
    }

    who {
      icon fa-male
      title who made this?
      path /who 
    }
  }

  jade @include(layout.jade)

}

flow redirect to https {
  http/redirect https://ui.reka.io
}

trigger {

  http ui.reka.io {
    run redirect to https
  }

  http ui.reka.io {
    ssl {
      crt @include(/root/reka.crt)
      key @include(/root/reka.key)
    }
    run http
  }

}
